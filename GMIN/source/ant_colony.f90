! LamarckiAnt
! Ant colony algorithm for GMIN
! Re-use as much GA stuff as possible
MODULE ANT_MODULE
   IMPLICIT NONE
   INTEGER ANT_BINS
   DOUBLE PRECISION ANT_BIN_WIDTH
   DOUBLE PRECISION, ALLOCATABLE :: ANT_TRAIL(:,:),NEW_ANT_TRAIL(:,:)
   DOUBLE PRECISION, ALLOCATABLE :: ANT_PROB(:,:),ANT_VIS_DIST(:,:)
   DOUBLE PRECISION, ALLOCATABLE :: ANT_Q(:)
   DOUBLE PRECISION :: ANT_GAMMA=3
   DOUBLE PRECISION :: ANT_TRAIL_WIDTH=0.0436332313056 ! 2.5 degrees
   DOUBLE PRECISION :: ANT_TRAIL_PERSISTENCE=0.1
   DOUBLE PRECISION :: ANT_BETA=0 ! Visibility
   INTEGER :: ANT_TRAIL_START=1
   DOUBLE PRECISION :: ANT_THETA=0.5
   LOGICAL :: ANT_DUMP=.FALSE.
END MODULE ANT_MODULE

SUBROUTINE ANT_ALGORITHM()
   USE MYGA_POPULATION
   USE ANT_MODULE
   USE COMMONS, ONLY : MCSTEPS,TSTART,MYUNIT,HIT,NRELAX,RESTART
   IMPLICIT NONE
   INTEGER I
   DOUBLE PRECISION MYGA_MEAN_ENERGY
   DOUBLE PRECISION TIME
   DOUBLE PRECISION ITMP
   DOUBLE PRECISION EPOCH_BEST
   INTEGER EPOCH_COUNT
   ! Set up
   CALL ANT_SETUP()
   EPOCH_BEST=1D6
   EPOCH_COUNT=0
   MYGA_COUNT_MIN=MYGA_NSTRUC
   IF (ANT_DUMP) THEN
      CALL ANT_DUMP_PATHS
      CALL ANT_DUMP_TRAILS
   ENDIF
   ! Main loop
   DO CURR_GEN=1,MYGA_GENS
      !WRITE(*,*) "Iteration ",CURR_GEN
      !CALL MYGA_PRINT_POP
      IF (MYGA_POP_ENERGY(1).LT.MYGA_POP_ENERGY(0)) THEN
         CALL COPY_STRUCTURE(1,0)
      ENDIF
   ! Update pheremone trails
      IF (RESTART.AND.(EPOCH_COUNT.GT.NRELAX)) THEN
         ANT_TRAIL=1/DBLE(ANT_BINS)
         MYGA_COUNT_EPOCH=MYGA_COUNT_EPOCH+1
         EPOCH_COUNT=0
         EPOCH_BEST=1D6
         WRITE(MYUNIT,'(A,I6)')"RESTART AT STEP ",CURR_GEN 
      ELSE
         CALL ANT_UPDATE_TRAILS
      ENDIF
   ! Generate new set of structures
      CALL MYGA_SORT(MYGA_NSTRUC*3+MYGA_NOFF*2)!Save old structures
      CALL MYGA_REMOVE_DUPLICATES(MYGA_NSTRUC*3+MYGA_NOFF*2,ITMP)
      DO I=1,MYGA_NSTRUC
         CALL COPY_STRUCTURE(I,I+2*(MYGA_NSTRUC+MYGA_NOFF))
      ENDDO
      CALL ANT_MAKE_PATHS
      DO I=1,MYGA_NSTRUC
         CALL IC2CART(I)
      ENDDO
    ! Optimise
      CALL MYGA_OPTIMISE_POP(1,MYGA_NSTRUC,MCSTEPS(1),MYGA_BQMAX)
      DO I=1,MYGA_NSTRUC
         CALL CART2IC(I)
      ENDDO
      CALL MYGA_SORT(MYGA_NSTRUC)
      CALL MYGA_OPTIMISE_POP(1,1,0,MYGA_CQMAX)
      IF (ANT_DUMP) THEN
         CALL ANT_DUMP_PATHS
         CALL ANT_DUMP_TRAILS
      ENDIF
      IF (MYGA_POP_ENERGY(1).LT.EPOCH_BEST) THEN
         EPOCH_BEST=MYGA_POP_ENERGY(1)
         EPOCH_COUNT=0
      ELSE
         EPOCH_COUNT=EPOCH_COUNT+1
      ENDIF
      CALL MYCPU_TIME(TIME)
      MYGA_COUNT_MIN=MYGA_COUNT_MIN+MYGA_NSTRUC*(1+MCSTEPS(1))
      WRITE(MYUNIT,'(A,I5,4(A,F15.6))') &
      "ANT> Generation=",CURR_GEN,&
      " Best=",MYGA_POP_ENERGY(1),&
      " Mean=",MYGA_MEAN_ENERGY(),&
      " Worst=",MYGA_POP_ENERGY(MYGA_NSTRUC),&
      " Time=",TIME-TSTART
      IF (HIT) THEN !Target hit
         WRITE(MYUNIT,'(A,I5)')"Target hit at generation",CURR_GEN
         GOTO 10
      ENDIF
   ENDDO
   ! Finished
10 CALL MYGA_OPTIMISE_POP(1,MYGA_NSTRUC,0,MYGA_CQMAX)
   CALL MYGA_FINALIO()
   STOP
END

SUBROUTINE ANT_SETUP()
   USE ANT_MODULE
   USE MYGA_PARAMS
   USE COMMONS, ONLY : NATOMS,MCSTEPS,MYUNIT,A_BLN, B_BLN, C_BLN, D_BLN
   IMPLICIT NONE
   INTEGER I,J
   DOUBLE PRECISION, PARAMETER :: PI = 3.141592654D0
   DOUBLE PRECISION TORSION
   DOUBLE PRECISION ENERGY,ETOT
   DOUBLE PRECISION:: EMAX=4.8
   ! Initial parameters
   CALL MYGA_SETUP()
   MYGA_NOFF=0
   MYGA_NMUT=0
   ANT_BINS=36
   ANT_BIN_WIDTH=2*PI/DBLE(ANT_BINS)
   ALLOCATE(ANT_TRAIL(ANT_BINS,NATOMS))
   ALLOCATE(NEW_ANT_TRAIL(ANT_BINS,NATOMS))
   ALLOCATE(ANT_PROB(ANT_BINS,NATOMS))
   ALLOCATE(ANT_VIS_DIST(ANT_BINS,NATOMS))
   ALLOCATE(ANT_Q(0:MYGA_NSTRUC))
   ! Initialise pheremone trails
   ANT_TRAIL=1/DBLE(ANT_BINS)
   ! Set up visibility distribution (flat probability distribution for now).
   ANT_VIS_DIST=1/DBLE(ANT_BINS)
   DO I=4,NATOMS
      ETOT=0D0
      DO J=1,ANT_BINS
         TORSION=2*PI*(J-0.5)/DBLE(ANT_BINS)
         ENERGY = A_BLN(i-2)*(1.0D0 + COS(TORSION)) + C_BLN(i-2)*(1.0 + cos(3.0*TORSION)) &
         + B_BLN(i-2)*(1.0D0 - COS(TORSION)) + D_BLN(i-2)*(1.0 + cos(TORSION+(PI/4.0)))
         ANT_VIS_DIST(J,I)=EMAX-ENERGY
         ETOT=ETOT+ANT_VIS_DIST(J,I)
!         WRITE(*,*)I,J,TORSION,ENERGY,ANT_VIS_DIST(J,I)
      ENDDO
      DO J=1,ANT_BINS !Renormalise visibility distribution
         ANT_VIS_DIST(J,I)=ANT_VIS_DIST(J,I)/ETOT
         !WRITE(*,*)I,J,TORSION,ANT_VIS_DIST(J,I)
      ENDDO
      !WRITE(*,*)
!      WRITE(*,*)I,I-3,A_BLN(I-2),B_BLN(I-2),C_BLN(I-2),D_BLN(I-2)
   ENDDO
   ! Generate first round of structures
   CALL ANT_MAKE_PATHS
   DO I=1,MYGA_NSTRUC
      CALL IC2CART(I)
   ENDDO
   CALL MYGA_OPTIMISE_POP(1,MYGA_NSTRUC,0,MYGA_BQMAX)
   DO I=1,MYGA_NSTRUC
      CALL CART2IC(I)
   ENDDO
   !CALL MYGA_PRINT_POP()
   CALL MYGA_SORT(MYGA_NSTRUC)
   WRITE(MYUNIT,'(A,I6)') "ANT> Number of ants:",MYGA_NSTRUC
   WRITE(MYUNIT,'(A,I6)') "ANT> Maximum number of generations:",MYGA_GENS
   WRITE(MYUNIT,'(A,F8.3)') "ANT> Gamma:",ANT_GAMMA
   WRITE(MYUNIT,'(A,F8.3)') "ANT> Trail width:",ANT_TRAIL_WIDTH
   WRITE(MYUNIT,'(A,F8.3)') "ANT> Trail persistence:",ANT_TRAIL_PERSISTENCE
   WRITE(MYUNIT,'(A,F8.3)') "ANT> Trail best structure rate:",ANT_THETA
   WRITE(MYUNIT,'(A,F8.3)') "ANT> Visibility constant:",ANT_BETA
   RETURN
END

SUBROUTINE ANT_MAKE_PATHS
   USE ANT_MODULE
   USE MYGA_PARAMS
   USE MYGA_POPULATION
   USE COMMONS , ONLY : NATOMS
   IMPLICIT NONE
   INTEGER I,J
   DOUBLE PRECISION, PARAMETER :: PI = 3.141592654D0
   DOUBLE PRECISION DPRAND
   INTEGER BIN
   INTEGER CURR_STRUC
   LOGICAL REJECTED
! Set up probability distribution
   DO I=1,ANT_BINS
      DO J=1,NATOMS
         ANT_PROB(I,J)=(1-ANT_BETA)*ANT_TRAIL(I,J)+ANT_BETA*ANT_VIS_DIST(I,J)
         !WRITE(*,*)ANT_PROB(I,J),(1-ANT_BETA)*ANT_TRAIL(I,J),ANT_BETA*ANT_VIS_DIST(I,J)
      ENDDO
   ENDDO
   DO CURR_STRUC = 1,MYGA_NSTRUC
      MYGA_POP_FOUND(CURR_STRUC)=CURR_GEN
! Set up bond distances
      DO J=2,NATOMS
         MYGA_POP_GENOME(3*J-2,CURR_STRUC)=1D0
      ENDDO
! Set up bond angles
      DO J=3,NATOMS
         MYGA_POP_GENOME(3*J-1,CURR_STRUC)=1.8326
      ENDDO
! Set up torsion angles
      DO J=4,NATOMS
         REJECTED=.TRUE.
         DO WHILE(REJECTED)
            MYGA_POP_GENOME(3*J,CURR_STRUC)=DPRAND()*2.*PI
            BIN=1+INT(MYGA_POP_GENOME(3*J,CURR_STRUC)/DBLE(ANT_BIN_WIDTH))
            IF (DPRAND().LT.ANT_PROB(BIN,J)) THEN
               REJECTED=.FALSE.
            ENDIF
            !WRITE(*,*)MYGA_POP_GENOME(3*J,CURR_STRUC),BIN,ANT_PROB(BIN,J),REJECTED
         ENDDO
      ENDDO
   ENDDO
   RETURN
END

SUBROUTINE ANT_DUMP_PATHS()
   USE ANT_MODULE
   USE MYGA_PARAMS
   USE MYGA_POPULATION
   USE COMMONS , ONLY : NATOMS
   IMPLICIT NONE
   INTEGER I,J,CURR_STRUC
   CHARACTER*10 FILENAME
   WRITE(FILENAME,'(I10)')CURR_GEN
   OPEN (50,file="ANT."//ADJUSTL(FILENAME))
   DO CURR_STRUC=1,MYGA_NSTRUC
      DO J=4,NATOMS
         !WRITE(50,*)J,MYGA_POP_GENOME(3*J,CURR_STRUC),MYGA_POP_GENOME(3*J,1)
      ENDDO
      WRITE(50,*)
   ENDDO
   CLOSE(50)
   RETURN
END

SUBROUTINE ANT_DUMP_TRAILS()
   USE ANT_MODULE
   USE MYGA_PARAMS
   USE MYGA_POPULATION
   USE COMMONS , ONLY : NATOMS
   IMPLICIT NONE
   INTEGER I,J,CURR_STRUC
   CHARACTER*10 FILENAME
   WRITE(FILENAME,'(I10)')CURR_GEN
   OPEN (50,file="PHEROMONE."//ADJUSTL(FILENAME))
   DO J=4,NATOMS
      DO I=1,ANT_BINS
         WRITE(50,'(I5,I5,F15.10)')I,J,ANT_TRAIL(I,J)
      ENDDO
      WRITE(50,*)
   ENDDO
   CLOSE(50)
   RETURN
END

SUBROUTINE ANT_UPDATE_TRAILS
   USE ANT_MODULE
   USE MYGA_PARAMS
   USE MYGA_POPULATION
   USE COMMONS , ONLY : NATOMS
   IMPLICIT NONE
   INTEGER I,J,K
   INTEGER CURR_STRUC
   DOUBLE PRECISION, PARAMETER :: PI = 3.141592654D0
   DOUBLE PRECISION DPRAND
   DOUBLE PRECISION EDIFF,THETA,ANGDIFF
   DOUBLE PRECISION FTMP
   ! Initialise new pheremone trail
   NEW_ANT_TRAIL=0D0
   ANT_Q=0D0
   ! Loop through ants
   DO CURR_STRUC=1,MYGA_NSTRUC
      ! Calculate learning rate for ant
      EDIFF=MYGA_POP_ENERGY(CURR_STRUC)-MYGA_POP_ENERGY(1)
      IF (EDIFF.LT.0)  EDIFF=0
      ANT_Q(CURR_STRUC)=EXP(-ANT_GAMMA*EDIFF)
      ANT_Q(0)=ANT_Q(0)+ANT_Q(CURR_STRUC)
   ENDDO
   ANT_Q(0)=ANT_Q(0)*ANT_THETA/(1-ANT_THETA)
   DO CURR_STRUC=ANT_TRAIL_START,MYGA_NSTRUC/10
      EDIFF=MYGA_POP_ENERGY(CURR_STRUC)-MYGA_POP_ENERGY(1)
      !WRITE(*,'(I5,F10.3,F10.7)') CURR_STRUC,EDIFF,ANT_Q(CURR_STRUC)
      ! Calculate contribution to each angle bin for each torsion
      DO J=4,NATOMS
         DO K=1,ANT_BINS
            THETA=(DBLE(K)-0.5)*ANT_BIN_WIDTH
            ANGDIFF=THETA-MYGA_POP_GENOME(3*J,CURR_STRUC)
            IF (ANGDIFF.GT.PI) THEN
               ANGDIFF=ANGDIFF-2*PI
            ELSEIF (ANGDIFF.LT.-PI) THEN
               ANGDIFF=ANGDIFF+2*PI
            ENDIF
            FTMP=EXP(-(ANGDIFF**2)/(2*(ANT_TRAIL_WIDTH**2)))
            FTMP=ANT_Q(CURR_STRUC)*FTMP/(PI*ANT_TRAIL_WIDTH*DSQRT(2D0))
            !WRITE(*,*)CURR_STRUC,J,THETA,MYGA_POP_GENOME(3*J,CURR_STRUC),ANGDIFF,FTMP
            NEW_ANT_TRAIL(K,J)=NEW_ANT_TRAIL(K,J)+FTMP
         ENDDO
      ENDDO
   ENDDO
   ! Renormalise ant trails
   DO I=4,NATOMS
      FTMP=0
      DO J=1,ANT_BINS
         FTMP=FTMP+NEW_ANT_TRAIL(J,I)
      ENDDO
      DO J=1,ANT_BINS
         NEW_ANT_TRAIL(J,I)=NEW_ANT_TRAIL(J,I)/FTMP
      ENDDO
   ENDDO
   ! Add new trails to existing trails
   DO I=4,NATOMS
      DO J=1,ANT_BINS
         ANT_TRAIL(J,I)=ANT_TRAIL_PERSISTENCE*ANT_TRAIL(J,I)+&
                (1-ANT_TRAIL_PERSISTENCE)*NEW_ANT_TRAIL(J,I)
      ENDDO
   ENDDO
   RETURN
END
